:icons: font
:source-highlighter: rouge
:toc:
:toc-placement!:

= The super keyword

toc::[]

Lets consider this example:

[source,lapyst]
----
shape Driveable
    var int wheels;

    def print_status()
        printf("wheels: %s\n", self.wheels);
    end
end

shape Car
    var int seats;

    def print_status()
        printf("seats: %s\n", self.seats);
    end
end
----

We want an method `print_status()` that prints out any fields of the object. But how do we call the `print_status()` of the parent?

One could guess we could write `self.print_status();` but this would only call the method itself, thus creating an infinite loop.

What we need is the `super` keyword. It's purpose is a bit like `self` but rather for inheritance: it references the parent shape.

[source,lapyst]
----
shape Driveable
    var int wheels;

    def print_status()
        printf("wheels: %s\n", self.wheels);
    end
end

shape Car use [Driveable]
    var int seats;

    def print_status()
        super.print_status();
        printf("seats: %s\n", self.seats);
    end
end
----

A alternative way to call a parents method is by just using the `super` keyword like an method. It's also the only way you can call the parent constructor inside the constructor.

[source,lapyst]
----
shape Driveable
    var int wheels;

    def !Driveable(int wheels)
        self.wheels = wheels;
    end
end

shape Car use [Driveable]
    var int seats;

    def !Car(int wheels, int seats)
        self.seats = seats;
        super(wheels); <1>
    end
end
----
<1> This line calls the constructor of `Driveable`. Note how it can be used even after other code.

== The deadly diamond

Lets consider the following example:

[source,lapyst]
----
shape A
    def display()
        printf("shape A display() called!\n");
    end
end

shape B use [A]
    def display()
        printf("shape B display() called!\n");
    end
end

shape C use [A]
    def display()
        printf("shape C display() called!\n");
    end
end

shape D use [B,C]

end

var D d = new D();
d.display(); <1>
----
<1> which `display()` method should be called?

Lapyst actually prevents this code form beeing compiled: if the compiler detects that there is an shape where it's parents both implement an identical method (this check respects overloading), then it's issues an error, and tells the programmer that he/she needs to implement the methods in question an the shape that inherit the methods. In our case here this would be the shape `D`:

[source,lapyst]
----
shape D use [B,C]

    def display()
        super[B].display(); <1>
        super[C].display();
    end

end
----
<1> Notice that this super has the parent's shape name in brackets, this tells lapyst that we want to call a method from an specific parent, here `B`.

NOTE: The `super` keyword not only supports to specify the parent you want to use, but also a way to specify ALL parents: `super[*].display()`. This only works for method calls and field assignments, but NOT on reading fields.

NOTE: The bracket syntax also works when using super as an method: `super[B]()` or `super[*]()`.