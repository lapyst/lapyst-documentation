:icons: font
:source-highlighter: rouge
:toc:
:toc-placement!:

= Generics

toc::[]

Sometimes it would be handy to have parameters on the shape itself wouldn't it? Thats what generics are for!

== Generic type parameters

Lets consider this example:

[source,lapyst]
----
shape MyContainer of [ T ] <1>
    var T content;
end
----
<1> the `of` keyword must come before the `use` keyword.

With the help of the `of` keyword we can express the generics for an shape. In the example above we define one named `T`.

This is an generic type that matches any type known to lapyst. With this the second line is possible: we define an field `content` of type `T`. But `T` itself is no real builtin class or shape! Instead it refers here to the generic type `T`. 

Now lets instanciate the shape:

[source,lapyst]
----
var MyContainer c = new MyContainer[int]();
----

As you see a new pair of brackets apeared: the square ones. They are for the generic parameters: for the generic type `T` we set the builtin type `int`. What we now create is an instance of `MyContainer` that has a field `content` of type `int`.

And thats where really the power of generics lay: with them we can make types we use in an shape variable, at least for the instaciation.

But we can go one step further: we can define that only types with an certain parent should be valid:

[source,lapyst]
----
shape Fruit
end

shape FruitBasket of [ T => Fruit ] <1>
    var T content;
end
----
<1> The `=&gt;` here declares that the generic type parameter `T` should have somewhare in it's parents the shape `Fruit`.

NOTE: This type-check always looks through the complete chain of anchestors of an type and looks if it finds there the type we declared, in our case `Fruit`.

Now let us use our `FruitBasket`:

[source,lapyst]
----
shape Apple use [Fruit]
end

shape SpecialApple use [Apple]
end

shape Carrot
end

// This is ok since 'Fruit' itself is an 'Fruit'
var FruitBasket[Fruit] fbasket = new FruitBasket[Fruit]();

// This one works too, since the parent of 'Apple' is 'Fruit'
var FruitBasket[Apple] abasket = new FruitBasket[Apple]();

// Also works, since 'SpecialApple' has the parent 'Apple',
// wich then has the parent 'Fruit'
var FruitBasket[SpecialApple] sabasket = new FruitBasket[SpecialApple]();

// This will throw an error, because `Carrot` dosn't have
// `Fruit` in their anchestors
var FruitBasket[Carrot] cbasket = new FruitBasket[Carrot]();
----

== Generic value parameters

Theres actually a second type of generic parameters we can use: values. They are essentially constants that are bound to the type of an instance.

Let's expand our `FruitBasket` shape from before to contain an maximum of fruits it can contain

[source,lapyst]
----
shape FruitBasket of [ T => Fruit , int maxSize ]
    var T content;

    def int getMaxSize()
        return maxSize; <1>
    end
end
----
<1> You can use this the same way as every other constant.

To write an generic value parameter, we must simply write it like an constant but without the 'const' keyword. If you want an type-free generic value parameter, you then once again NEED the `const` keyword:

[source,lapyst]
----
shape FruitBasket of [ T => Fruit , const maxSize ] <1>
end
----
<1> Now `maxSize` is an type-less generic value parameter.

== Generic arguments for an parent type

Assume we have following shape:
[source,lapyst]
----
shape MyBase of [ T ]
    # ...
end
----

and we now want to extend `MyBase`, but for that must give the type an generic argument. We do this like we do it when we refer to the type elsewhere:

[source,lapyst]
----
shape MyExt use [ MyBase[int] ] <1>
    # ...
end
----
<1> this declares the parent `MyBase` to use the type `int` for its generic parameter `T`.

But what if we dont know the type we should give your parent, because our extended shape has a generic type parameter as well?

We then simply can reuse this parameter inside the parent declaration:
[source,lapyst]
----
shape MyExt of [ T ] use [ MyBase[T] ]
    # ...
end
----

NOTE: If your parent generic type parameter has an lower limit (via a `=>` declaration), then your generic argument in the extened shape needs at least this limit as well. If you dont declare this, lapyst treads this as an error.