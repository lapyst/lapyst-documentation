:icons: font
:source-highlighter: rouge
:toc:
:toc-placement!:

= Templates / Classes

toc::[]

Lapyst is a so called Object-Oriented programming language. But what is an object? Short: everything is an object, but when we speak of object we typically mean user-defined objects. They are "containers" for data and simplify the handling of data like functions do for code.

Lapyst follows here the "role" or "behavior" pattern, meaning a class dosnt dictate to 100% how a object in lapyst should be build, but rather it defines one part of the object.

Lets look at a simple template:

[source,lapyst]
----
template Animal
    // ...
end
----

The `template` keyword starts a new template, it then follows the name of the template which is an identifier and follows the same rule as all identifiers.

To instantiate a new instance of this template, we use the `new` keyword:

[source,lapyst]
----
var Animal a = new Animal();
----

Also notice how the type of the variable is now the name of our template: this means we now typed our variable as an `Animal`, meaning we also get the same type-checks as for builtin types but now also for our custom defined one!

== Fields

We now know how to write a template and how we create an instance of it, but we currently dont store anthing in it. Theres where fields come into play: they are like variables but they reside inside a instance of an template, while declared on the template itself:

[source,lapyst]
----
template Animal
    var int weight; <1>
end
----
<1> This defines a field with the name `weight` of type `int`.

With this field itself we cannot do much, since fields in lapyst can have an visibility, which defaults to private. This is also the case with the field we just created. So lets make it public:

[source,lapyst]
----
template Animal
    var int !weight; <1>
end
----
<1> The `!` before the name declares the field as public.

Now it's public so we can assign data to it and also read from it:

[source,lapyst]
----
var Animal a = new Animal();
a.weight = 12;
printf("weight: %d", a.weight); <1>
----
<1> This should print `weight: 12`.

Theres actually a 3rd type of visibility: protected. But for that we first need to understand inheritance.

== Methods

Methods a like functions but they are bound to an template / object and operate on them.
We declare them inside the template like we would normal functions:

[source,lapyst]
----
template Animal
    
    def make_noise()
        // ...
    end

end
----

When then can simply call them like so:

[source,lapyst]
----
var Animal a = new Animal();
a.make_noise(); <1>
----
<1> This calls the `make_noise` method from the `Animal` template the instance stored in `a`.

Methods also supports the same parameter and return functionalities we know from functions.

=== Static methods

A problem with methods is we cannot use them without an instance of the template. But fear not: theres an solution to this problem: static methods! Static methods are essentially methods (or functions) that you can call without an instance of the template.

To make an method static, you use the `static` keyword after the `def` keyword:

[source,lapyst]
----
template Car
    def (int) !getWheels()
        return 4;
    end

    def static (bool) !canHaveWheels()
        return true;
    end
end

Car.getWheels();        # this fails

Car.canHaveWheels();    # this works
----

// self keyword
include::templates/self.adoc[leveloffset=+1]

// constructor
include::templates/constructor.adoc[leveloffset=+1]

== Inheritance

Templates are all cool but what if we have two templates, say `Cat` and `Dog` which have many things in common? It would be a bit tedious to write them both from scratch isn't it? Lucky for us that we have inheritance! It allows us to declare a "parent" template for each template which then inherit all fields and methods of the parent:

[source,lapyst]
----
template Animal

    var int weight;

    def make_noise()
        // ...
    end

end

template Dog use [Animal]
    // ...
end

template Cat use [Animal]
    // ...
end

var Cat = new Cat();
cat.weight = 11;
cat.make_noise();
----

Use see, with `use` we can declare the parent(s) of an template. Why plural you may ask? Because lapyst supports multi-inheritance:

[source,lapyst]
----
template Mammal
    // ...
end

template Fish
    // ...
end

template Dolphin use [Mammal, Fish]
    // ...
end
----

This makes it possible for the `Dolphin` template to inherit fields and methods from both `Mammal` and `Fish`.

== Protected fields

As we quickly note when we talked about fields, there is a third level of visibility next to private and public: protected. But first lets look on how the other two behave with inheritance:

[source,lapyst]
----
template Animal
    var int weight;
    var int !height;
end

template Cat use [Animal]

    def show_fields()
        // this works, since 'height' is public
        printf("heigth: %d", self.height);

        // this dosn't work, since 'weight' is is
        // only visible to 'Animal' itself
        printf("weight: %d", self.weight);
    end

end
----

As we see, we have a bit of a problem here: private fields are only visible inside the template they are declared on, but public fields are not only accessible by templates that inherit from its parent but also for the complete outside world.

Then it's time for protected fields:

[source,lapyst]
----
template Animal
    var int *weight;
end

template Cat use [Animal]

    def show_fields()
        printf("weight: %d", self.weight);
    end

end
----
<1> The `&#42;` before the name declares the field as protected.

Protected means that only the class it's declared on and its "children" can access it.

In each other language that would be it, but lapyst takes the whole thing a step further: you can declare how many levels of inheritance a protected field is visible. This is done by the amount of asterix's (`&#42;`) before the fieldname:

[source,lapyst]
----
template Animal
    var int *weight;
    var int **height; <1>
end

template Cat use [Animal]
    // ...
end

template Tiger use [Cat]
    def show_fields()
        // dosnt work since the `weight` field is only visible for one level
        printf("weight: %d", self.weight);

        // work since the `height` field is visible to up to two levels
        printf("height: %d", self.height);
    end
end
----
<1> Notice how theres no whitespace between the `&#42;` itself and also between the `&#42;` and the name.

As shown above, the level is counted from the template that define the protection onwards.

// Super-keyword
include::templates/super.adoc[leveloffset=+1]

// Generics
include::templates/generics.adoc[leveloffset=+1]

== Re-Entry of Templates

Lapyst supports re-entry into all template-contexts like ruby does. But what does "re-entry" (or "re-opening") means?
It simply means that you can re-open the template after the `end`-keyword that closes the template to add more stuff to it:

[source, lapyst]
----
template Animal <1>
    var int height;
end <2>

# ... some other code ...

template Animal <3>
    def (int) !getHeight()
        return self.height;
    end
end
----
<1> this "opens" the template `Animal` for the first time, thus declaring it
<2> this "closes" the template `Animal`
<3> here we "re-open" the template `Animal`. This does NOT overwrite anything we've done before!

IMPORTANT: inheritance as well as generics can only be defined by the *FIRST* opening of an template. Generics (`as`) or inheritance (`use`) declerations on re-opening's will result in an syntax error.

Re-entry also allows us to "overwrite" previous methods. But this time not only through overloading, but real "overwriting", meaning the original method can completly be replaced with the new one. To save the methods, use the `alias_method` macro:

[source,lapyst]
----
template MyList
    def push(string element)
        printf("original MyList.push %s", element);
    end
end

# ...

template MyList
    alias_method(:_old_push, :push); <1>

    def push(string element) <2>
        _old_push(element + ' test');
        printf("new MyList.push %s", element);
    end
end

var l = new MyList();
l.push('hello world');
----
<1> This aliases the method the method body of `push` with a new name: `_old_push`. This does *not* remove the old name!
<2> Here we associate a new method with the name `push`. This would also be the case without a previous `alias_method`.

Overwriting of an method only works in the context of an re-openend template.
An second definition of `push` with the same signature like below produces an syntax error:
[source,lapyst]
----
template MyList
    def push(string element)
        # ...
    end

    def push(string element)    # this line will trigger the syntax error
        # ...
    end
end
----