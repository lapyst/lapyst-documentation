:icons: font
:source-highlighter: rouge
:toc:
:toc-placement!:

= The super keyword

toc::[]

Lets consider this example:

[source,lapyst]
----
template Driveable
    var int wheels;

    def print_status()
        printf("wheels: %s\n", self.wheels);
    end
end

template Car
    var int seats;

    def print_status()
        printf("seats: %s\n", self.seats);
    end
end
----

We want an method `print_status()` that prints out any fields of the object. But how do we call the `print_status()` of the parent?

One could guess we could write `self.print_status();` but this would only call the method itself, thus creating an infinite loop.

What we need is the `super` keyword. It's purpose is a bit like `self` but rather for inheritance: it references the parent template.

[source,lapyst]
----
template Driveable
    var int wheels;

    def print_status()
        printf("wheels: %s\n", self.wheels);
    end
end

template Car use [Driveable]
    var int seats;

    def print_status()
        super.print_status();
        printf("seats: %s\n", self.seats);
    end
end
----

== The deadly diamond

Lets consider the following example:

[source,lapyst]
----
template A
    def display()
        printf("template A display() called!\n");
    end
end

template B use [A]
    def display()
        printf("template B display() called!\n");
    end
end

template C use [A]
    def display()
        printf("template C display() called!\n");
    end
end

template D use [B,C]

end

var D d = new D();
d.display(); <1>
----
<1> which `display()` method should be called?

Lapyst actually prevents this code form beeing compiled: if the compiler detects that there is an template where it's parents both implement an identical method (this check respects overloading), then it's issues an error, and tells the programmer that he/she needs to implement the methods in question an the template that inherit the methods. In our case here this would be the template `D`:

[source,lapyst]
----
template D use [B,C]

    def display()
        super[B].display(); <1>
        super[C].display();
    end

end
----
<1> Notice that this super has the parent's template name in brackets, this tells lapyst that we want to call a method from an specific parent, here `B`.

NOTE: The `super` keyword not only supports to specify the parent you want to use, but also a way to specify ALL parents: `super[*].display()`. This only works for method calls and field assignments, but NOT on reading fields.